# Maintainer: Alexey Rochev <equeim@gmail.com>

pkgname=tremotesf-git
pkgver=2.4.0.r48.g4522728
pkgrel=1
pkgdesc="Remote GUI for transmission-daemon"
url="https://github.com/equeim/tremotesf2"
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'kwidgetsaddons' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'cmake' 'ninja' 'gettext' git)
source=("${pkgname%-git}::git+https://github.com/equeim/tremotesf2.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
 mkdir -p build
}

build() {
  cd "${pkgname%-git}/build"
  cmake .. \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  ninja
}

package() {
  cd "${pkgname%-git}/build"
  DESTDIR="$pkgdir" ninja install
}
