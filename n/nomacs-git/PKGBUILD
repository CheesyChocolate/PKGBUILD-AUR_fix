# Maintainer: Peter Mattern <pmattern at arcor dot de>

_pkgname=nomacs
pkgname=$_pkgname-git
pkgver=3.19.0.r2.gf2c6c4d3
pkgrel=1
pkgdesc='Free, open source image viewer, which supports multiple platforms. Qt6 Lolix pkgbuild'
arch=('i686' 'x86_64')
url='https://github.com/nomacs/nomacs'
license=('GPL3')
depends=(exiv2 gcc-libs glibc libraw libtiff opencv qt6-base qt6-svg)
#depends+=(libopencv_imgproc.so)
makedepends=('git' git-lfs 'cmake' 'qt6-tools' python)
optdepends=('qt6-imageformats: support additional image formats'
            'kimageformats: support QOI (Quite OK Image Format)')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/$_pkgname/$_pkgname.git"
        #"git+https://github.com/nomacs/nomacs-plugins.git"
        )
sha256sums=('SKIP')

export GIT_LFS_SKIP_SMUDGE=1

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  # https://github.com/nomacs/nomacs/issues/951
  #patch -Np1 -i ../nomacs-fix-exiv2-0.28.patch.txt
  # copy plugin sources into place
  #cp -av "${srcdir}/nomacs-plugins/"* "ImageLounge/plugins"
}

build() {
  cd nomacs
  cmake -B build -S ImageLounge -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6 \
    -DUSE_SYSTEM_QUAZIP=ON \
    -DENABLE_AVIF=ON \
    -DENABLE_TRANSLATIONS=ON

  cmake --build build
}

package() {
  cd nomacs
  DESTDIR="$pkgdir" cmake --install build
}
