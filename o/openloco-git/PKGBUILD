# Maintainer: Lolix

pkgname=openloco-git
pkgver=24.07.r4.g509bcc144
pkgrel=1
pkgdesc="An open source re-implementation of Chris Sawyer's Locomotion"
arch=(x86_64 i686)
url="https://github.com/OpenLoco/OpenLoco"
license=(MIT)
depends=(sdl2 libpng openal)
depends_x86_64+=(lib32-glibc lib32-gcc-libs lib32-sdl2 lib32-libpng lib32-openal)
makedepends=(cmake yaml-cpp git gtest fmt)
makedepends_x86_64+=(lib32-gtest lib32-yaml-cpp lib32-fmt)
optdepends_x86_64=(
    'lib32-libpipewire: audio output'
    'lib32-libpulse: audio output'
)
optdepends_i686=(
    'libpipewire: audio output'
    'libpulse: audio output'
)
provides=(openloco)
conflicts=(openloco)
options=(!lto)
source=("git+https://github.com/openloco/openloco.git"
	    "openloco.desktop")
sha256sums=('SKIP'
            '57512f00144c1e0d2cc91c3adbf38460d5ec1223afc27bd16e1271760bce02ae')

pkgver() {
  cd openloco
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	local _flags=(
    -DFETCHCONTENT_QUIET:BOOL=OFF
    -DFETCHCONTENT_FULLY_DISCONNECTED:BOOL=ON
    #-Dfmt_DIR:PATH=/usr/lib/cmake/fmt
    -Dsfl_DIR:PATH=/usr/include/sfl
    -Dyaml-cpp_DIR:PATH=/usr/lib/cmake/yaml-cpp
	)

  export CXXFLAGS="$CXXFLAGS -m32 -Wno-error=null-dereference"
  cmake -G "Unix Makefiles" -B build -S "openloco" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  install -Dm644 "openloco.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  DESTDIR="${pkgdir}" cmake --install build
  install -D "openloco/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
