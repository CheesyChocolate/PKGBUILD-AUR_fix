# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=qt6-webkit-movableink-git
pkgver=r284069.9a33154f9d7a
pkgrel=1
pkgdesc="Classes for a WebKit2 based implementation and a new QML API"
arch=(x86_64)
url="https://github.com/movableink/webkit"
license=(LGPL2.1)
depends=(qt6-base qt6-location libjpeg libpng libwebp libxml2 hyphen harfbuzz harfbuzz-icu icu woff2 zlib
         gstreamer gst-plugins-base-libs libxslt

         # namcap implicit depends
         glibc glib2 gcc-libs sqlite libgcrypt libtasn1)
depends+=(libicuuc.so libicui18n.so)
makedepends=(git cmake ruby ruby-erb gperf python qt6-tools libxcomposite
    gst-libav
    gst-plugins-base
    gst-plugins-good
    gst-plugins-ugly
    gst-plugins-bad
    gst-plugins-bad-libs
)

optdepends=('gst-plugins-good: Webm codec support')
provides=(qt6-webkit) #qt6-webkit-movableink
conflicts=(qt6-webkit) #qt6-webkit-movableink
options=(!lto)
source=("movableink-webkit::git+https://github.com/movableink/webkit.git"
        qtwebkit-ruby3.2.patch)
sha256sums=('SKIP'
            '977221aca9471072d64a46e7541a0d3c540387841f47bb05b0af6c2fa25b034d')

prepare() {
  cd movableink-webkit
  patch -Np1 -i ../qtwebkit-ruby3.2.patch
  echo "Done patch for Ruby 3.2"
}

pkgver() {
  cd "movableink-webkit"
  #git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S movableink-webkit -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
