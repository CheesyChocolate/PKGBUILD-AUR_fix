# Maintainer: robertfoster

pkgname=gst-plugins-rs-git
pkgver=r3049.8a6bcb71
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=(x86_64)
url="https://gstreamer.freedesktop.org/"
license=(Apache LGPL2.1 MIT MPL2)
depends=(gstreamer gtk4 dav1d libsodium libwebp)
makedepends=(git rust meson cargo-c clang hotdoc nasm)
provides=(gst-plugins-rs)
conflicts=(gst-plugins-rs)
options=(!lto)
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git")
sha256sums=(SKIP)

pkgver() {
  cd "${pkgname%-git}"
  #git describe --long --tags --exclude  0.1* | sed 's/^foo-//;s/\([^-]*-g\)/r\1/;s/-/./g'
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  #cargo fetch --target x86_64-unknown-linux-gnu --manifest-path "${srcdir}/${pkgname%-git}/Cargo.toml"
}

build() {
  cd "${pkgname%-git}"
  arch-meson build \
    -D csound=enabled \
    -D dav1d=disabled \
    -D sodium=enabled

  meson compile -C build
}

package() {
  cd "${pkgname%-git}"
  meson install -C build --destdir "${pkgdir}"
  install -D LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
