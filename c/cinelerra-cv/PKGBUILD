# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributer: Geballin - Guillaume Ballin <macniaque at free dot fr>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=cinelerra-cv
pkgver=2.3+380+gbbcaf2fa
pkgrel=1
epoch=2
pkgdesc="Professional video editing and compositing environment - Community version"
arch=(x86_64)
url="https://github.com/cinelerra-gg/cinelerra-cv/"
license=(GPL)
depends=(e2fsprogs libavc1394 libiec61883 libxv
         libtiff mjpegtools fftw a52dec glu
         ffmpeg faad2 faac openexr libxft)
makedepends=(git automake nasm mesa intltool gcc9)
source=("git+https://github.com/cinelerra-cv-team/cinelerra-cv.git#commit=bbcaf2fa70d6acb0ab764e853d50e6aa04e16496"
        "cinelerra-cv-binutils-fix.patch::https://github.com/cinelerra-cv-team/cinelerra-cv/commit/18a693425f78f7c4c68b5a342efce3e8db2a30dc.patch")
sha512sums=('c68f0da963f986e304d0644e1648f8b76a402f1e569c050dc0d18fd918df0a029e0d9bc55cc08c13c053c45b84e1f4554bb4c1db15af50653eaebf77ad547799'
            '34112d09de13b195b1ca87aec2ed7fd0c6378d0fe39e09e186509295509ee4ce89c5f39d4b8106eb546d37d43479fe3041d83d77bdfab986340fcbe5a9ccc77a')

export CC=/usr/bin/gcc-9 CXX=/usr/bin/g++-9

pkgver() {
  cd "${pkgname}"
  git describe --long | sed 's/^v//g;s/-/+/g'
}

prepare() {
  cd "${pkgname}"
  patch -Np1 -i ../cinelerra-cv-binutils-fix.patch
  ./autogen.sh
}

build() {
  export CFLAGS+="  -Wno-incompatible-pointer-types"
  export CXXFLAGS+="  -Wno-incompatible-pointer-types"

  cd "${pkgname}"
  # disable mmx due to improper use of registers in asm
  # - possibly a new problem since gcc 4.9
  ./configure pkg_config='pkg-config --static' \
    --prefix=/usr \
    --with-buildinfo=git \
    --enable-opengl \
    --disable-mmx \
    --disable-esd
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,NEWS,README.BUILD,TODO}
}
